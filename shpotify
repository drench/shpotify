#!/usr/bin/env zsh

zmodload zsh/stat

full_cmd=$0
cmd=$(basename "$full_cmd")

warn() { >&2 echo $* }
die() { warn $*; exit 111 }

SHPOTIFY_CONFIG_DIR=${SHPOTIFY_CONFIG_DIR:-~/.config/shpotify}
env_dir="$SHPOTIFY_CONFIG_DIR/.env.d"
[[ -e "$env_dir" ]] || die "${env_dir} does not exist!"
[[ -z "$(ls -A "$env_dir")" ]] && die "${env_dir} is empty!"
for env_file in $env_dir/*; do
  key=$(basename "$env_file")
  value=$(cat "$env_file")
  export "$key=$value"
done

spotify_api_get() {
  refresh_token_if_outdated
  curl --silent -H "Authorization: Bearer ${SPOTIFY_ACCESS_TOKEN}" \
    "https://api.spotify.com/v1/$1" | jq .
}

fetch_refresh_token() {
  curl \
    --silent \
    -d "client_id=${SPOTIFY_CLIENT_ID}" \
    -d "client_secret=${SPOTIFY_CLIENT_SECRET}" \
    -d "grant_type=refresh_token" \
    -d "refresh_token=${SPOTIFY_REFRESH_TOKEN}" \
    https://accounts.spotify.com/api/token | jq -r '.access_token'
}

refresh_token_if_outdated() {
  token_mtime=$(zstat +mtime "$env_dir/SPOTIFY_ACCESS_TOKEN")
  now=$(date +%s)
  delta=$(($now - $token_mtime))
  [[ "$delta" -lt 3000 ]] && return
  warn "Refreshing the spotify access token"
  new_token=$(fetch_refresh_token)
  [[ "$new_token" = "null" ]] && die "No token returned"
  [[ -z "$new_token" ]] && die "Token is empty ???"
  echo "$new_token" > "$env_dir/SPOTIFY_ACCESS_TOKEN"
  export "SPOTIFY_ACCESS_TOKEN=$new_token"
}

actions=(me)

action=$1
case "$action" in
  me) spotify_api_get me ;;
  --completion) printf "%s\n" $actions ;;
  --completion-code)
    cmd=$(basename "$full_cmd")
    echo "compctl -K _${cmd} ${cmd}"
    echo "_${cmd}() {"
    echo '  local words; read -cA words; shift -p words'
    echo '  reply=($($words --completion))'
    echo '}'
    ;;
  *) die "Usage: $full_cmd [ $actions ]" ;;
esac
